apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "free.rm.skytube"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 9
        buildConfigField 'String[]', 'YOUTUBE_API_KEYS',
                '{' +
                        '"AIzaSyBK7NLymFzs-P8j8jT7kjeWkY0yXdue8_g",' +
                        '"AIzaSyC23oUYMmy2hlmxSJNvo5UyfvmNmAJ_Lao",' +
                        '"AIzaSyA-Y12I5tlGUcodUyH-RQHn94F7c60Y4d0"'  +
                '}';
        buildConfigField 'String', 'SKYTUBE_WEBSITE',         '"http://skytube-app.com"'
        buildConfigField 'String', 'SKYTUBE_WEBSITE_CREDITS', '"http://skytube-app.com/credits.html"'
        buildConfigField 'String', 'SKYTUBE_UPDATES_URL',     '"https://api.github.com/repos/ram-on/SkyTube/releases/latest"'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true    // this is as we have over 65536 lines of code...
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CHROMECAST_APP_ID", "\"39E8D587\""
        }
        debug {
            buildConfigField "String", "CHROMECAST_APP_ID", "\"29A66E17\""
        }
    }
    flavorDimensions "skytube"
    productFlavors {
        // 100% Open-Source Edition
        oss {
            applicationId "free.rm.skytube.oss"
            versionName "2.60"          // the digits of the *fractional part* shall not be larger than "9"
            resValue "string", "app_name", "SkyTube"
            dimension "skytube"
        }
        // 99% Open-source edition:  uses the YouTube Player Jar [i.e. proprietary module - optional usage]
        extra {
            applicationId "free.rm.skytube.extra"
            versionName "2.60 Extra"    // the digits of the *fractional part* shall not be larger than "9"
            resValue "string", "app_name", "SkyTube Extra"
            dimension "skytube"
        }
    }
    dexOptions {
        jumboMode true
    }
}

configurations {
    nonFreeCompile
    compile.exclude group: "org.apache.httpcomponents", module: "httpclient"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    extraCompile files('libs/YouTubeAndroidPlayerApi.jar')
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'com.google.http-client:google-http-client-android:1.22.0'
    compile 'com.google.apis:google-api-services-youtube:v3-rev181-1.22.0'
    // libextractor
    compile 'org.jsoup:jsoup:1.10.1'
    compile 'org.mozilla:rhino:1.7.7'

    compile 'com.github.moraisigor:slidingdrawer:1.5.5'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.4'
    compile 'com.afollestad.material-dialogs:core:0.9.0.2'
    compile 'com.github.angads25:filepicker:1.1.1'

    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:multidex:1.0.2'    // this is as we have over 65536 lines of code...
    compile 'joda-time:joda-time:2.9.4'
    extraCompile 'com.android.support:mediarouter-v7:26.1.0'
    extraCompile 'com.google.android.gms:play-services-cast-framework:11.4.0'
    extraCompile 'com.googlecode.android-query:android-query:0.25.9'
    extraCompile 'com.sothree.slidinguppanel:library:3.4.0'
    extraCompile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    extraCompile 'com.google.apis:google-api-services-youtube:v3-rev178-1.22.0'
}
